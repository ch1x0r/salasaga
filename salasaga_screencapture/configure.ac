AC_INIT(salasaga_screencapture, [0.8.0-alpha3], [salasaga-devel@lists.sourceforge.net])

MAJOR_VERSION=0
MINOR_VERSION=8
MICRO_VERSION=0-alpha3

SALASAGA_VERSION=[${MAJOR_VERSION}.${MINOR_VERSION}.${MICRO_VERSION}]

dnl Tell configure to look in the config subdirectory
dnl for config.guess, config.sub, etc
AC_CONFIG_AUX_DIR(config)

dnl config.h needs to be generated
AC_CONFIG_HEADERS(config.h)

dnl 2008-07-18 JC Testing out Automake stuff again
dnl Specify the version of automake needed
AM_INIT_AUTOMAKE(1.8)

dnl GNU recommended way to determine host variables (OS, etc)
AC_CANONICAL_HOST

AC_PROG_CC
AC_PROG_INSTALL

dnl Check for the zlib library
AC_CHECK_LIB(z, compress2, ZLIB="-lz")

dnl Windows has the zlib library, but its named zdll instead
dnl Note the smoke-and-mirrors setting of HAVE_LIBZ if it's found. :) (should allow MinGW to work)
AC_CHECK_LIB(zdll, compress2, ZLIB="-lzdll")
dnl 2008-07-18 JC Had to comment this out for autoheader to work
dnl AC_CHECK_LIB(zdll, compress2, AC_DEFINE([HAVE_LIBZ]))

dnl Stop the warnings generated by autogen.sh
AC_USE_SYSTEM_EXTENSIONS

dnl Turn on GNU gettext support
AM_GNU_GETTEXT
AM_GNU_GETTEXT_VERSION([0.17])

dnl Check for the GTK, GLib, GConf, Gnome, xml, and png libraries
AC_CHECK_PROG(PKG_CONFIG, pkg-config, [yes])
if test x$PKG_CONFIG = xyes;
then
	GTK_CFLAGS=`pkg-config glib-2.0 gdk-2.0 gdk-pixbuf-2.0 gtk+-2.0 gconf-2.0 libgnome-2.0 libpng libxml-2.0 --cflags`
	GTK_LIBS=`pkg-config glib-2.0 gdk-2.0 gdk-pixbuf-2.0 gtk+-2.0 gconf-2.0 libgnome-2.0 libpng libxml-2.0 --libs`
fi

case "${host_os}" in
	*darwin*)
		SHLIBLDFLAGS="-dynamiclib \
			-compatibility_version \
			\`expr 1 + \"\$(MAJOR_VERSION)\"\`.\$(MINOR_VERSION) \
			-current_version \
			\`expr 1 + \"\$(MAJOR_VERSION)\"\`.\$(MINOR_VERSION) \
			-install_name \$(libdir)/\$(SHAREDLIB)"
		SHLIBEXT=".dylib"
		CFLAGS="$CFLAGS -fno-common"
		;;
	*linux*)
		SHLIBLDFLAGS="-shared -Wl"
		SHLIBEXT=".so"
		SHCFLAGS="-fPIC"
		;;
	*solaris*)
 		SHLIBLDFLAGS="-shared -Wl"
		SHLIBEXT=".so"
 		SHCFLAGS="-fPIC"
		;;
	mingw*)
		LDFLAGS="-mwindows"
		SHLIBEXT=".dll"
		SHLIBLDFLAGS="-shared -Wl"
		;;
	*)
		SHLIBLDFLAGS="-shared"
		SHLIBEXT=".so"
esac

AC_SUBST(GTK_CFLAGS)
AC_SUBST(GTK_LIBS)
AC_SUBST(LDFLAGS)
AC_SUBST(SHLIBEXT)
AC_SUBST(SHLIBLDFLAGS)
AC_SUBST(SHCFLAGS)
AC_SUBST(ZLIB)
AC_SUBST(MAJOR_VERSION)
AC_SUBST(MINOR_VERSION)
AC_SUBST(MICRO_VERSION)
AC_SUBST(SALASAGA_VERSION)

AC_CONFIG_FILES(Makefile intl/Makefile po/Makefile.in)

AC_OUTPUT()
